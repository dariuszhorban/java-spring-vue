plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.0'
	id 'io.spring.dependency-management' version '1.1.5'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-websocket'
	implementation 'com.squareup.okhttp3:okhttp:4.9.3'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	implementation 'org.json:json:20210307'
}

tasks.register('npmInstall', Exec) {
	workingDir 'src/frontend'
	commandLine 'npm', 'install'
}

tasks.register('npmBuild', Exec) {
	workingDir 'src/frontend'
	dependsOn 'npmInstall'
	commandLine 'npm', 'run', 'build'
}

tasks.register('copyFrontendToBuild', Copy) {
	dependsOn 'npmBuild'
	from file('src/frontend/dist')
	into file("$buildDir/resources/main/static")
}

processResources {
	dependsOn 'copyFrontendToBuild'
}
